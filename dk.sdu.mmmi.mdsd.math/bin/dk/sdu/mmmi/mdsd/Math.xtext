grammar dk.sdu.mmmi.mdsd.Math with org.eclipse.xtext.common.Terminals

generate math "http://www.sdu.dk/mmmi/mdsd/Math"

MathExp:
	program=Program (externals+=External*)? variables += VarBinding*
;

External:
	'external' name=ID '(' (parameters+=ParmeterTypes ("," parameters+=ParmeterTypes)*)? ')'
;

enum ParmeterTypes:
	int='int' | string='string'
;

Program:
	'program' name=ID
;

VarBinding:
	'var' name=ID '=' expression=Exp
;

Exp returns Expression:
	Factor (( {Plus.left=current} '+' | {Minus.left=current}  '-' ) right=Factor)*
;

Factor returns Expression:
	Primary (({Mult.left=current} '*' | {Div.left=current}  '/'  ) right=Primary)*
;

Primary returns Expression:
	{MathNumber} value=INT  | Parenthesis  |  VariableUse | LetBinding | ExternalUse
;

ExternalUse:
	ref=[External] '(' (expressions+=Exp ("," expressions+=Exp)*)? ')'
;

LetBinding:
	'let' name=ID '=' binding=Exp 'in' body=Exp 'end'
;

Parenthesis:
	'(' expression=Exp ')'
;

Binding:
	VarBinding | LetBinding
;

VariableUse:
	ref = [Binding]
;
